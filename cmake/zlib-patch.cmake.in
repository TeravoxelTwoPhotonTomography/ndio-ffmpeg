# For MSVC x64: a patch is required to deal with static libraries 
# that use an rc file (such a zlibstatic)
# SEE: http://www.cmake.org/Bug/print_bug_page.php?bug_id=11240
#
# Patch taken from: http://code.google.com/p/eathena-project/source/browse/trunk/3rdparty/zlib/Patch.cmake.in?spec=svn15122&r=15122
# Date: 2012-09-04
#
# Requires:
# _SOURCE_DIR - path to the CMakeLists.txt file to patch
set( CMAKE_SIZEOF_VOID_P @CMAKE_SIZEOF_VOID_P@ )
set( MSVC @MSVC@ )
set( APPLE @APPLE@ ) 
set( _SOURCE_DIR "@_SOURCE_DIR@" )

set( _TARGET "${_SOURCE_DIR}/CMakeLists.txt" )
if( NOT (EXISTS "${_TARGET}") )
  message( FATAL_ERROR "Cannot find ${_TARGET}" )
endif()

file( READ "${_TARGET}" _DATA )
# Remove --version-script linker flag on OS X
if(APPLE)
  string(REPLACE "set_target_properties(zlib PROPERTIES LINK_FLAGS" "#===PATCH - remove" _DATA "${_DATA}")
endif()
# Remove CMAKE_DEBUG_POSTFIX
string( REPLACE "set(CMAKE_DEBUG_POSTFIX \"d\")" "#=== Patch - remove CMAKE_DEBUG_POSTFIX ===" _DATA "${_DATA}" )
string( REPLACE "if(NOT MINGW)" "if(NOT MINGW AND NOT CYGWIN)#=== Patch - fix Cygwin compilation ===" _DATA "${_DATA}" )
string( REPLACE "if(MINGW)" "if(MINGW OR CYGWIN)#=== Patch - fix Cygwin compilation ===" _DATA "${_DATA}" )
list( APPEND _DATA "#=== Patch - append BEGIN ===\n" )
# Normalize library name
if(MSVC)
  list( APPEND _DATA
    "set_target_properties(zlibstatic PROPERTIES PREFIX \"\")\n"
    "set_target_properties(zlibstatic PROPERTIES OUTPUT_NAME zlibstatic)\n"
    "set_target_properties(zlibstatic PROPERTIES SUFFIX .lib)\n" )
endif()
if( CMAKE_SIZEOF_VOID_P EQUAL 8 AND MSVC )
  # Error detected with generator Visual Studio 10 Win64
  # > LINK : warning LNK4068: /MACHINE not specified; defaulting to X86
  # > fatal error LNK1112: module machine type 'x64' conflicts with target machine type 'X86'
  #
  # There is no default value for static libraries and cmake isn't setting it either.
  # We fix this by adding the flag manually.
  list( APPEND _DATA "set_target_properties(zlibstatic PROPERTIES STATIC_LIBRARY_FLAGS \"/machine:x64\")\n" )
endif()
list( APPEND _DATA "#=== Patch - append END ===\n" )
file( WRITE "${_TARGET}" ${_DATA} )